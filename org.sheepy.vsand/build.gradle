import org.gradle.internal.os.OperatingSystem

plugins {
	id 'application'
	id 'distribution'
	id 'org.mini2Dx.butler'
}

switch ( OperatingSystem.current() ) {
	case OperatingSystem.WINDOWS:
		project.ext.OS = "windows"
		break
	case OperatingSystem.LINUX:
		project.ext.OS = "linux"
		break
	case OperatingSystem.MAC_OS:
		project.ext.OS = "macos"
		break
}

task jlink(type: Exec, dependsOn: ['installDist']) {
	def javaHome = System.properties.'java.home'
	workingDir "$buildDir"
	commandLine "$javaHome/bin/jlink",
		'--strip-debug',
		'--no-header-files',
		'--no-man-pages',
		'--exclude-files', '**java_*.properties',
		'--compress', '2',
		'--add-modules', "java.base,java.xml,jdk.unsupported,openj9.sharedclasses",
		'--output', 'image'
}

task cleanJlink(type: Delete) {
	delete "$buildDir/image"
}

task copyReleng(type: Copy) {
	from "releng/$project.ext.OS"
	into "$buildDir/image"
}

task copyModules(type: Copy) {
	from "$buildDir/install/org.sheepy.vsand/lib/"
	into "$buildDir/image/modules"
}

tasks.clean.dependsOn(cleanJlink)
tasks.jlink.dependsOn(cleanJlink)
tasks.jlink.finalizedBy(copyReleng)
tasks.jlink.finalizedBy(copyModules)


butler {
	user = "ealrann"
	game = "vsand"
	userVersion = project.version

	windows {
		binDirectory = "$projectDir/build/image"
		channel = "windows"
	}
	linux {
		binDirectory = "$projectDir/build/image"
		channel = "linux"
	}
}

mainClassName = 'org.sheepy.vsand/org.sheepy.vsand.VSandApplicationLauncher'

test {
	useJUnitPlatform()
}

dependencies {

	implementation 'org.sheepy.lily.vulkan:org.sheepy.lily.vulkan.api'
	implementation 'org.sheepy.lily.vulkan:org.sheepy.lily.vulkan.extra.api'

	runtimeOnly 'org.sheepy.lily.vulkan:org.sheepy.lily.vulkan.base'
	runtimeOnly 'org.sheepy.lily.vulkan:org.sheepy.lily.vulkan.extra.nuklear'


	testImplementation "org.junit.jupiter:junit-jupiter-api:$junitJupiterVersion"
	testImplementation "org.junit.jupiter:junit-jupiter-params:$junitJupiterVersion"

	testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher:$junitPlatformVersion"
}

sourceSets {
	main {
		java.srcDirs = ['src/generated/java',
			'src/main/java',
			'src/main/shader']
		resources.srcDirs = ['src/main/resources']
	}
}
