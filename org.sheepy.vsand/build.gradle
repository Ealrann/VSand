import org.gradle.internal.os.OperatingSystem

plugins {
	id 'application'
	id 'distribution'
	id 'org.mini2Dx.butler'
	id 'org.beryx.jlink' version '2.11.1'
}

switch ( OperatingSystem.current() ) {
	case OperatingSystem.WINDOWS:
		project.ext.OS = "windows"
		break
	case OperatingSystem.LINUX:
		project.ext.OS = "linux"
		break
	case OperatingSystem.MAC_OS:
		project.ext.OS = "macos"
		break
}

task copyReleng(type: Copy) {
    from "releng/$project.ext.OS"
    into 'build/image'
}

jlink {
		moduleName = 'org.sheepy.vsand'
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages',
    			'--exclude-files', '**java_*.properties']
    launcher{
        name = 'VSand'
        jvmArgs = ['-Dlog4j.configurationFile=./log4j2.xml']
    }
}

tasks.jlink.finalizedBy(copyReleng)

butler {
  user = "ealrann"
  game = "vsand"
  userVersion = project.version

  windows {
	binDirectory = "$projectDir/build/image"
    channel = "windows"
  }
  linux {
	binDirectory = "$projectDir/build/image"
    channel = "linux"
  }
}

mainClassName = 'org.sheepy.vsand/org.sheepy.vsand.VSandApplicationLauncher'
jar {
    manifest {
        attributes 'Implementation-Title': "modular-hello",
                'Main-Class': 'org.sheepy.vsand.VSandApplicationLauncher'
    }
}

test {
  useJUnitPlatform()
}

dependencies {
	implementation project(':org.sheepy.lily.core.impl')

	implementation project(':org.sheepy.lily.vulkan.api')
	implementation project(':org.sheepy.lily.vulkan.process.compute')
	implementation project(':org.sheepy.lily.vulkan.process.graphic')

	implementation project(':org.sheepy.lily.vulkan.extra.api')
	implementation project(':org.sheepy.lily.vulkan.extra.graphic')

	implementation project(':org.sheepy.lily.vulkan.extra.nuklear')

	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
	testImplementation 'org.junit.jupiter:junit-jupiter-params:5.4.2'

	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

sourceSets {
	main {
		java.srcDirs = ['src/generated/java',
			'src/main/java']
		resources.srcDirs = ['src/main/resources']
	}
}
