import org.gradle.internal.os.OperatingSystem

plugins {
	id 'java'
	id 'application'
	id 'distribution'
	id 'org.beryx.jlink' version '2.7.0'
}

jlink {
		moduleName = 'org.sheepy.vsand'
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher{
        name = 'VSand'
        jvmArgs = ['-Dlog4j.configurationFile=./log4j2.xml']
    }
}

mainClassName = 'org.sheepy.vsand/org.sheepy.vsand.VSandApplicationLauncher'
jar {
    manifest {
        attributes 'Implementation-Title': "modular-hello",
                'Main-Class': 'org.sheepy.vsand.VSandApplicationLauncher'
    }
}

startScripts {
    applicationName = 'VSand'
}

dependencies {
	implementation project(':org.sheepy.lily.core.impl')

	implementation project(':org.sheepy.lily.vulkan.api')
	implementation project(':org.sheepy.lily.vulkan.process.compute')
	implementation project(':org.sheepy.lily.vulkan.process.graphic')

	implementation project(':org.sheepy.lily.vulkan.nuklear')
}

switch ( OperatingSystem.current() ) {
	case OperatingSystem.WINDOWS:
		project.ext.OS = "windows"
		break
	case OperatingSystem.LINUX:
		project.ext.OS = "linux"
		break
	case OperatingSystem.MAC_OS:
		project.ext.OS = "macos"
		break
}

distributions {
	main {
		baseName = "VSand_$OS"
	}
}

sourceSets {
	main {
		java.srcDirs = ['src/generated/java',
			'src/main/java']
		resources.srcDirs = ['src/main/resources']
	}
}
