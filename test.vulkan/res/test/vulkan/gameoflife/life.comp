#version 450
#extension GL_ARB_separate_shader_objects : enable

#define WIDTH 800
#define HEIGHT 600
#define WORKGROUP_SIZE 32

layout (local_size_x = WORKGROUP_SIZE, local_size_y = WORKGROUP_SIZE, local_size_z = 1) in;

layout(binding = 0) readonly buffer buf1 {
   int data[];
} previousBoard;

layout(binding = 1) buffer buf2 {
   int data[];
} nextBoard;

void main() {

  uint x = gl_GlobalInvocationID.x;
  uint y = gl_GlobalInvocationID.y;
  uint arrayLocation = WIDTH * y + x;
  uint countNeighboors = 0;
  
  if (y > 0)
  {
    if (x > 0)
    {
      // x - 1 ; y - 1
      countNeighboors += previousBoard.data[arrayLocation - 1 - WIDTH];
    }
    
    // x ; y - 1
    countNeighboors += previousBoard.data[arrayLocation- WIDTH];
    
    if(x < WIDTH - 1)
    {
      // x + 1 ; y - 1
      countNeighboors += previousBoard.data[arrayLocation + 1 - WIDTH];
    }
  }
  
  if (x > 0)
  {
  	// x - 1 ; y
  	countNeighboors += previousBoard.data[arrayLocation - 1];
  }
  
  if(x < WIDTH - 1)
  {
    // x + 1 ; y
    countNeighboors += previousBoard.data[arrayLocation + 1];
  }
  
  if (y < HEIGHT - 1)
  {
    if (x > 0)
    {
      // x - 1 ; y + 1
      countNeighboors += previousBoard.data[arrayLocation - 1 + WIDTH];
    }
    
    // x ; y + 1
    countNeighboors += previousBoard.data[arrayLocation + WIDTH];
    
    if(x < WIDTH - 1)
    {
      // x + 1 ; y + 1
      countNeighboors += previousBoard.data[arrayLocation + 1 + WIDTH];
    }
  }
  
  if(countNeighboors == 3)
  {
  	nextBoard.data[arrayLocation] = 1;
  } 
  else if (countNeighboors == 2) 
  {
  	nextBoard.data[arrayLocation] = previousBoard.data[arrayLocation];
  }
  else 
  {
  	nextBoard.data[arrayLocation] = 0;
  }
}
