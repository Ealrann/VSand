#version 450
#extension GL_ARB_separate_shader_objects : enable

#define WIDTH 1024
#define HEIGHT 512
//#define WIDTH 2048
//#define HEIGHT 1152
#define WORKGROUP_SIZE 32

#define SQUARE 0
#define CIRCLE 1

layout (local_size_x = WORKGROUP_SIZE, local_size_y = WORKGROUP_SIZE, local_size_z = 1) in;

layout(binding = 0) buffer boardBuffer
{
	int data[WIDTH][HEIGHT];
}board;

layout(binding = 1) uniform attributeBuffer
{
	int shape;
	int size;
	int value;
	int x;
	int y;

	int squareSize;
}attributes;

bool testCircle(int x, int y);

void main() {
	if (gl_GlobalInvocationID.x >= WIDTH || gl_GlobalInvocationID.y >= HEIGHT)
		return;

	int x = int(gl_GlobalInvocationID.x);
	int y = int(gl_GlobalInvocationID.y);

	if (x >= attributes.x - attributes.size
			&& y >= attributes.y - attributes.size
			&& x <= attributes.x + attributes.size
			&& y <= attributes.y + attributes.size) {

		bool isInShape = false;
		switch (attributes.shape) {
		case SQUARE:
			isInShape = true;
			break;
		case CIRCLE:
			isInShape = testCircle(x, y);
			break;
		}

		if (isInShape) {
			board.data[x][y] = attributes.value;
		}
	}
}

bool testCircle(int x, int y) {
	int distX = (attributes.x - x) * (attributes.x - x);
	int distY = (attributes.y - y) * (attributes.y - y);

	return (distX + distY) <= (attributes.squareSize);
}
