#version 450
#extension GL_ARB_separate_shader_objects : enable

#define WIDTH 1024
#define HEIGHT 576
#define WORKGROUP_SIZE 32

layout (local_size_x = WORKGROUP_SIZE, local_size_y = WORKGROUP_SIZE, local_size_z = 1) in;

struct Entry {
	int isStatic;
	int density;
	int runoff;
	float r;
	float g;
	float b;

	// Padding for the alignement
	float padding1;
	float padding2;
};

layout(binding = 0) uniform SConfiguration{
	Entry materials[16];
}configuration;

layout(binding = 1) buffer buf2 {
	int data[WIDTH][HEIGHT];
}board;

layout(binding = 2) buffer buf3 {
	bool data[WIDTH][HEIGHT];
}moved;

void main() {

	uint x = gl_GlobalInvocationID.x;
	uint y = (gl_GlobalInvocationID.y * 2) - 1;

	int value1 = y > 0 ? board.data[x][y] : 0;
	int value2 = y < (HEIGHT - 1) ? board.data[x][y + 1] : 0;

	bool alreadyMoved1 = y > 0 ? moved.data[x][y] : false;
	bool alreadyMoved2 = y < (HEIGHT - 1) ? moved.data[x][y + 1] : false;

	if (configuration.materials[value1].isStatic == 0
			&& configuration.materials[value2].isStatic == 0
			&& alreadyMoved1 == false && alreadyMoved2 == false) {
		if (configuration.materials[value1].density
				> configuration.materials[value2].density) {

			if (y > 0) {
				board.data[x][y] = value2;
				moved.data[x][y] = true;
			}
			if (y < HEIGHT - 1) {
				board.data[x][y + 1] = value1;
				moved.data[x][y + 1] = true;
			}
		}
	}
}
