#version 450

#define WIDTH 1280
#define HEIGHT 720

#define WORKGROUP_SIZE_X 32
#define WORKGROUP_SIZE_Y 16

#define UP    0
#define DOWN  1
#define RIGHT 2
#define LEFT  3

#define TO_UP    1 << 0
#define TO_DOWN  1 << 1
#define TO_RIGHT 1 << 2
#define TO_LEFT  1 << 3

#define RANDOM_1_LOC  8
#define RANDOM_2_LOC  10
#define RANDOM_3_LOC  12
#define RANDOM_4_LOC  14

layout (local_size_x = WORKGROUP_SIZE_X, local_size_y = WORKGROUP_SIZE_Y, local_size_z = 1) in;

struct Entry
{
	int isStatic;
	int density;
	int runoff;
	float viscosity;
	float r;
	float g;
	float b;

	// Padding for the alignement
	float padding1;
};

// The definition of materials.
layout(binding = 0) uniform SConfiguration
{
	Entry materials[16];
}configuration;

// Random number, change each frame.
layout(binding = 1) uniform FrameUniformBuffer
{
	uint random;
}FUB;

// The game board, with all the values.
layout(binding = 2) readonly buffer SBoard
{
	int data[WIDTH][HEIGHT];
}board;

// DecisionBoard store necessary informations around movement.
// Each 32 bit value (one per cell) stores:
// 8 bits for the initial value
// 8 bits for the swapped value
// 8 bits with a random value.
//		This value is divided in 4 value of 2 bit, and represent
// 		a random array containing all the unique values : 0, 1, 2 and 3.
//		Used to have a favourite neighboors per cells.
// 3 unused bits.
// 1 bit, true if the cell is swapped.
// 4 bits for the direction we intend to swap.
layout(binding = 3) buffer SDecision
{
	uint data[WIDTH][HEIGHT];
}decision;

int check(int x, int y, int runoff, int dir, int srcDensity);

/**
 * Here, we will choose one favourite destination.
 */
void main()
{
	int x = int(gl_GlobalInvocationID.x);
	int y = int(gl_GlobalInvocationID.y);

	int currentValue = board.data[x][y];
	// Erase Tag,
	// Erase previous value
	// and Store the current value to the decision buffer.
	decision.data[x][y] = (((decision.data[x][y] >> 8) << 24) >> 16) | (currentValue << 24);

	if (configuration.materials[currentValue].isStatic == 0)
	{
		int density = configuration.materials[currentValue].density;

		int valueDown = y < (HEIGHT - 1) ? board.data[x][y + 1] : 0;
		// Make a list of available directions

		if (configuration.materials[valueDown].isStatic == 0
				&& density > configuration.materials[valueDown].density)
		{
			decision.data[x][y] |= TO_DOWN | (valueDown << 16);
		}
		else
		{
			// If not fallingdown, we check the right/left move
			int valueLeft = x > 0 ? board.data[x - 1][y] : 0;
			int valueRight = x < (WIDTH - 1) ? board.data[x + 1][y] : 0;

			int runoff = configuration.materials[currentValue].runoff;
			int distLeft = runoff;
			int distRight = runoff;

			if (configuration.materials[valueLeft].density < density)
			{
				distLeft = check(x - 1, y + 1, runoff, -1, density);
			}
			if (configuration.materials[valueRight].density < density)
			{
				distRight = check(x + 1, y + 1, runoff, 1, density);
			}

			bool right = distRight < runoff;
			bool left = distLeft < runoff;
			int targetValue = 0;

			if (right || left)
			{
				uint tag = 0;
				if (right && left)
				{
					if (distRight < distLeft)
					{
						tag = TO_RIGHT;
						targetValue = valueRight;
					}
					else if (distRight > distLeft)
					{
						tag = TO_LEFT;
						targetValue = valueLeft;
					}
					else // if (distLeft == fistRight)
					{
						// We prefer left side. Right is naturally favourized due to
						// the course of the shader, left to right.
						if (mod(FUB.random, 3) == 0)
						{
							tag = TO_RIGHT;
							targetValue = valueRight;
						}
						else
						{
							tag = TO_LEFT;
							targetValue = valueLeft;
						}
					}
				}
				else
				{
					if (right)
					{
						tag = TO_RIGHT;
						targetValue = valueRight;
					}
					else if (left)
					{
						tag = TO_LEFT;
						targetValue = valueLeft;
					}
				}
				decision.data[x][y] |= tag | (targetValue << 16);
			}
		}
	}
}

int check(int x, int y, int runoff, int dir, int srcDensity)
{
	int course = 0;
	if (y >= 0 || y < HEIGHT)
	{
		while (runoff != 0)
		{
			if (x >= 0 && x < WIDTH)
			{
				int value = board.data[x][y];
				if (configuration.materials[value].isStatic == 0
						&& configuration.materials[value].density < srcDensity)
				{
					break;
				}
			}
			else
			{
				break;
			}
			x += dir;
			runoff--;
			course++;
		}
	}

	return course;
}
